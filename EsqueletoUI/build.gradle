apply plugin: 'android'

android {
    final PROVIDER_DEBUG = "com.esqueleto.esqueletoui.debug.provider"
    final PROVIDER_RELEASE = "com.esqueleto.esqueletoui.provider"

    compileSdkVersion 19
    buildToolsVersion "19.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName '1.0'
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file('../distribution/debug.keystore')
        }
    }

    buildTypes {
        release {
            resValue "string", "authority", PROVIDER_RELEASE
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            resValue "string", "authority", PROVIDER_DEBUG
            runProguard false
            packageNameSuffix ".debug"
            signingConfig signingConfigs.debug
            versionNameSuffix "-debug"
        }

        staging {
            packageNameSuffix ".staging"
            versionNameSuffix "-staging"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }
        debug {
            java.srcDirs += ['src/debug/java']
            setRoot('build-types/debug')
        }
        staging.setRoot('build-types/staging')
        release.setRoot('build-types/release')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    compile project(':EsqueletoSDK')
    compile 'com.android.support:appcompat-v7:+'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile ('ly.apps:android-rest:1.2.1-SNAPSHOT@aar') {
        transitive = true
        changing = true
    }
}
